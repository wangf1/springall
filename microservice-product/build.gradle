plugins {
    id "groovy"
    id "org.springframework.cloud.contract" version "4.1.4"
    id "maven-publish"
}

group = 'com.wangf.spring'
version = '0.0.1-SNAPSHOT'

ext {
    cloudContractVersion = "4.1.4"
    groovyVersion = "4.0.22"
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:${cloudContractVersion}"
    }
}


dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

    runtimeOnly 'com.h2database:h2'

    testImplementation "org.apache.groovy:groovy-all:${groovyVersion}"
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
}


contracts {
    baseClassForTests = "com.wangf.spring.product.contract.BaseContractTestClass"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact bootJar

            artifact verifierStubsJar

            // https://github.com/spring-gradle-plugins/dependency-management-plugin/issues/273
            versionMapping {
                usage("java-api") {
                    fromResolutionOf("runtimeClasspath")
                }
                usage("java-runtime") {
                    fromResolutionResult()
                }
            }
        }
    }
}